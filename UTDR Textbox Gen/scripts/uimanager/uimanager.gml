#region Macros
	#macro SYSTEMUI obj_system //System object
	#macro UI_MESSAGE !SYSTEMUI.ui_paused //For pausing ui elements
	#macro FACE_CURRENT obj_system.dial_face[obj_system.dial_text_page] //Get the current dialogue face
	#macro FACE_INDEX obj_system.dial_face_index[obj_system.dial_text_page] //Get the current dialogue face index
	#macro FACE_SPEED obj_system.dial_face_spd[obj_system.dial_text_page] //Get the current dialogue face speed
	#macro FACE_ORIGINAL obj_system.dial_face_original[obj_system.dial_text_page] //Get the original dialogue face
	#macro FACE_PREVIOUS obj_system.dial_face_prev[obj_system.dial_text_page] //Get the previous dialogue face
	#macro FACE_INTERNAL obj_system.dial_face_name[obj_system.dial_text_page] //Get the internal name for the current dialogue face
	#macro FACE_USING FACE_CURRENT != -1 && FACE_CURRENT != 0 //If the dialogue box will contain a face
	soup_store("android", $"{""}{PATHSEP}", , true);
	#macro LAST_SAVED $"{!is_android() ? executable_get_directory() : soup_checkout("android", false, true)}latest_soupy_last_typed.soupy" //Last text we typed
	#macro AUTO_ASTERISK ( ( obj_system.dial_text_halign == 0 && obj_system.dial_text_valign == 0 ) && obj_system.dial_point_auto && string_trim(obj_system.dial_point_chr) != "" ) //Whether to enable auto-asterisk
	#macro PATHSEP (( os_type == os_windows || os_type == os_xboxseriesxs || os_type == os_gdk ) ? "\\"  :  "/") //Get platform-dependant path
	#macro PREF_SOUP $"{!is_android() ? executable_get_directory() : soup_checkout("android", false, true)}soupy_preferences.soupy" //Settings to save
	#macro GAME_VERSION "1.6.8" //Current game version
#endregion
///@desc Help Scribble with how to align the text
function scribble_alignment(halign_ = 0, valign_ = 0) {
	var align_ = { h: halign_, v: valign_, };
	switch ( halign_ ) {
		case 0: case "0": case "left": case "begin": case "start":				{ align_.h = "pin_left"; } break;
		case 1: case "1": case "center": case "centre": case "middle":	{ align_.h = "pin_center"; } break;
		case 2: case "2": case "right": case "end":										{ align_.h = "pin_right"; } break;
		default: { align_.h = "pin_left"; } break;
	}
	
	switch ( valign_ ) {
		case 0: case "0": case "top": case "up":											{ align_.v = "pin_top"; } break;
		case 1: case "1": case "center": case "centre": case "middle":	{ align_.v = "pin_middle"; } break;
		case 2: case "2": case "bottom": case "down":								{ align_.v = "pin_bottom"; } break;
		default: { align_.v = "pin_top"; } break;
	}
	
	return align_;
}

#region Default functions for the menu buttons
	function on_enter_() { if ( SYSTEMUI.ui_tab != id_ ) { sfx_play(snd_sel_switch); TweenFire("~ocirc", "$15", "yoff>", 5); text = $"[c_yellow][wheel]{text_static}"; color_butt = c_yellow; } }
	function on_enter_a() { if ( SYSTEMUI.ui_tab != id_ ) { sfx_play(snd_sel_switch); TweenFire("~ocirc", "$15", "yoff>", 5); text = $"[c_lime][wheel]{text_static}"; color_butt = c_lime; } }
	function on_leave_() { if ( SYSTEMUI.ui_tab != id_ ) { TweenFire("~ocirc", "$15", "yoff>", 0); text = text_static; color_butt = SYSTEMUI.ui_accentcolor; } window_set_cursor(cr_default); }
	function on_click_() { if ( SYSTEMUI.ui_tab != id_ ) { sfx_play(snd_select); SYSTEMUI.ui_tab = id_; on_reset_(); } else { sfx_play(snd_bump, , , random_range(0.8, 1.2)); } }
	function on_hover_() { window_set_cursor(cr_drag); }
	function on_reset_(update_ = true) { 
		if ( !instance_exists(SYSTEMUI) ) { exit; }
		SYSTEMUI.ui_reset(update_);
		
		var i = 0;
		repeat ( array_length(SYSTEMUI.butt) ) { with ( SYSTEMUI.butt[i].data ) { if ( SYSTEMUI.ui_tab != id_ ) { TweenFire("~ocirc", "$15", "yoff>", 0); text = text_static; color_butt = SYSTEMUI.ui_accentcolor; } else { TweenFire("~ocirc", "$15", "yoff>", 5); text = $"[c_yellow][wheel]{text_static}"; color_butt = c_yellow; } } i++; }
	}
#endregion

#region Context Menu Functions
	///@desc Clears all text in the textbox
	function soupy_context_clear() { textinput.SetValue(""); sfx_play(snd_throw); dial_updatet = 1; }
	
	///@desc Inserts a page break in the textbox
	function soupy_context_page() { 
		var txt_ = textinput.GetValue(), cursor_ = textinput.GetCaret() + 1, insert_ = "[/page]";
		txt_ = string_insert(insert_, txt_, cursor_); textinput.SetValue(txt_);
		sfx_play(snd_bump); dial_updatet = 1; textinput.SetCaret(( cursor_ + string_length(insert_) ) - 1); 
	}
	
	///@desc Adds textbox contents as a new macro
	function soupy_context_macro() { 
		var txt_ = textinput.GetValue();
		
		var arr_ = [
			new LuiText({ value: "Add textbox contents as a new macro for reuse?", text_halign: fa_center, text_valign: fa_middle, font: fnt_abaddon, color: c_white, xoff: 0, y: 10 }),
			new LuiText({ value: "See all your macros in the Extras tab! Scroll to the bottom.", text_halign: fa_center, text_valign: fa_middle, font: fnt_abaddon, color: c_white, xoff: 0, y: 10 }),
			new LuiText({ value: "Labels must be uniquely named.", text_halign: fa_center, text_valign: fa_middle, font: fnt_abaddon, color: c_white, xoff: 0, y: 10 }),
			new LuiInput({ height: 40, placeholder: "Label (ex: uty_clover, wavyrainbow, soupytext, etc.)", offset: 12, type_sfx: snd_txttype, color_normal: c_white, color_hover: c_gray, }).addEvent(LUI_EV_CREATE, function(e_) { soup_store("label", e_, , true); }),
			new LuiButton({ text: "Add new macro!", height: 40, }).addEvent(LUI_EV_CLICK, function () { 
				var result = soup_checkout("label", false, true).get();
				if ( string_trim(string_lettersdigits(result)) == "" ) { soupy_message("You cannot have a|blank or invalid label.", , 270, , , snd_error, , , true); exit; }
				
				var available = is_undefined(global.pref.macros[$ result]), text_ = SYSTEMUI.textinput.GetValue();
				if ( string_trim(string_lettersdigits(text_)) == "" ) { soupy_message("Your textbox doesn't have|any text to add!", , 270, , , snd_error, , , true); exit; }
				
				if ( available ) { sfx_play(snd_sparkle2); sfx_play(snd_chest); global.pref.macros[$ result] = text_; SYSTEMUI.save_pref(); soup_checkout("mainui", , true).destroy(); SYSTEMUI.ui_paused = false; }
				else { soupy_message("A macro with this|label already exists.", , 270, , , snd_error, , , true); }
			}),
		];
		
		var mainui = soupy_popup(arr_, , "Cancel", , , , snd_dimbox, fnt_abaddon); soup_store("mainui", mainui, , true);
	}
#endregion

///@desc Saves the latest dialogue text for crash recovery.
function soupy_save_last_typed(text_) {
	var text_bytes_ = string_byte_length(text_);
	var lasttyped = buffer_create(max(1, text_bytes_), buffer_fixed, 1);
	if ( text_bytes_ > 0 ) { buffer_write(lasttyped, buffer_text, text_); }
	buffer_save(lasttyped, LAST_SAVED);
	buffer_delete(lasttyped);
}

function TextChange(txt, point) : UndoableChange() constructor { //Handle undo/ redoing changes
	//live_auto_call
	prev_txt = SYSTEMUI.dial_text; //Store previous/ inital text
	point_prev = SYSTEMUI.textinput.GetCaret(); //Get previous point
	mytxt = txt; //Get our new text
	point_ = point; //Get our new point

	static can_apply = function() { return ( SYSTEMUI.dial_text != mytxt ); } //Don't push the same unchanged text to the undo stack
	static apply = function() { with ( obj_system ) { //Apply recent changes
		dial_text = other.mytxt; dial_text_page_c = scribble(dial_text).get_page_count();
		textinput.SetValue(dial_text);
		soupy_save_last_typed(dial_text);
		
		textinput.SetCaret(other.point_); } 
		sfx_play(snd_updated); 
	}
	static undo = function() { with ( obj_system ) {
		dial_text = other.prev_txt; dial_text_page_c = scribble(dial_text).get_page_count();
		textinput.SetValue(dial_text);
		soupy_save_last_typed(dial_text);
		textinput.SetCaret(other.point_prev);
	} sfx_play(snd_throw); }
}

///@desc Create a GUI button. Accepts { x, y, text, padd_(x1, y1, x2, y2, multi), leeway, x2, y2, sprite, draw_nine, index, (x)(y)scale, angle, font, color, color_butt, halign, and valign, and functions for on_enter(runs once), on_hover, on_leave(once), on_click(once), on_held, on_released(once) }
///@param {struct} datastruct_ Data struct for button functionality.
function Button(datastruct_ = undefined) constructor {
	data = datastruct_; button = undefined; on_enter = false; on_leave = false;
	if ( is_undefined(data[$ "text"]) ) { data[$ "text"] = ""; }
	data[$ "text_static"] = data.text;

	static update = function() {
		var y2pd = data[$ "padd_y2"] ?? 0, x2pd = data[$ "padd_x2"] ?? 0, y1pd = data[$ "padd_y1"] ?? 0, x1pd = data[$ "padd_x1"] ?? 0, paddmulti = data[$ "padd_multi"] ?? 0, hastext = data.text != "";
		if ( hastext ) {
		button = scribble(data.text)
						.align(data[$ "halign"] ?? fa_center, data[$ "valign"] ?? fa_middle)
						.starting_format(data[$ "font"] ?? "fnt_determination_nomono", data[$ "color"] ?? c_white)
						.scale(data[$ "scale"] ?? 1)
						.padding(x1pd + paddmulti, y1pd + paddmulti, x2pd + paddmulti, y2pd + paddmulti);
		}
			
		var spr_ = data[$ "sprite"] ?? spr_pixel, index = data[$ "index"] ?? 0, clrbutt = data[$ "color_butt"] ?? c_white, clrbutthovered = data[$ "color_butt_hover"] ?? data.color_butt, angle = data[$ "angle"] ?? 1, xscale = data[$ "xscale"] ?? 1, yscale = data[$ "yscale"] ?? 1, scalemulti = data[$ "scale"] ?? 1, x_ = data[$ "x"] ?? 0, y_ = data[$ "y"] ?? 0, x2_ = data[$ "x2"] ?? 0, y2_ = data[$ "y2"] ?? 0, xoff = data[$ "xoff"] ?? 0, yoff = data[$ "yoff"] ?? 0, bbox = hastext ? button.get_bbox(x_, y_) : ({ left: x_, top: y_, right: x2_, bottom: y2_, width: x2_ - x_, height: y2_ - y_ }), leeway = data[$ "leeway"] ?? 5;
		var visible_ = data[$ "draw_nine"] ?? true;
		var hovered = range_within(mouse_x_gui, bbox.left - leeway, bbox.right + leeway) && range_within(mouse_y_gui, bbox.top - leeway, bbox.bottom + leeway);
		if ( visible_ ) { draw_sprite_stretched_ext(spr_, index, bbox.left + xoff, bbox.top + yoff, bbox.width, bbox.height, !hovered ? clrbutt : clrbutthovered, 1); } //Draw nine-slice sprite
		else { draw_sprite_ext(spr_, index, bbox.left + xoff, bbox.top + yoff, xscale * scalemulti, yscale * scalemulti, angle, !hovered ? clrbutt : clrbutthovered, 1); } //Draw normal sprite
		if ( hastext ) { button.draw(x_ + xoff, y_ + yoff); } //Draw Scribble text
			
		if ( hovered && window_has_focus() ) { //If the mouse has enter within the bounding box
			if ( UI_MESSAGE ) { 
				if ( !on_enter ) { on_enter = true; on_leave = false; if ( !is_undefined(data[$ "on_enter"]) && data[$ "on_enter"] != -1 ) { data[$ "on_enter"](); } } //Mouse Entered Function
				if ( !is_undefined(data[$ "on_hover"]) && data[$ "on_hover"] != -1 ) { data[$ "on_hover"](); } //Mouse Hovering Function
				if ( mouse_pressed && !is_undefined(data[$ "on_click"]) && data[$ "on_click"] != -1 ) { data[$ "on_click"](); exit; } //Mouse Pressed Function
				if ( mouse_check && !is_undefined(data[$ "on_held"]) && data[$ "on_held"] != -1 ) { data[$ "on_held"](); } //Mouse Held Function
				if ( mouse_released && !is_undefined(data[$ "on_released"]) && data[$ "on_released"] != -1) { data[$ "on_released"](); } //Mouse Released Function
				if ( mouse_pressed_right && !is_undefined(data[$ "on_click_right"]) && data[$ "on_click_right"] != -1 ) { data[$ "on_click_right"](); exit; } //Mouse Pressed Function
				if ( mouse_check_right && !is_undefined(data[$ "on_held_right"]) && data[$ "on_held_right"] != -1 ) { data[$ "on_held_right"](); } //Mouse Held Function
				if ( mouse_released_right && !is_undefined(data[$ "on_released_right"]) && data[$ "on_released_right"] != -1 ) { data[$ "on_released_right"](); } //Mouse Released Function
			}
		}
		else { //If the mouse just left the bounding box
			if ( on_enter ) { if ( !on_leave ) { on_leave = true; on_enter = false; if ( !is_undefined(data[$ "on_leave"]) && data[$ "on_leave"] != -1 ) { data[$ "on_leave"](); } } } //Mouse Leave Function
		}
	}
}

///@desc Platform-based url opening
function soupy_url(path_, args_ = "", act_ = "", cmd_ = 5, webview_ = true) {
	if ( !is_android() ) { if ( !is_wasm() ) { execute_shell_simple(path_, args_, act_, cmd_); } else { url_open(path_); } } 
	else { if ( webview_ ) { if ( !is_android_wasm() ) { webview_open_url(path_); webview_allow_swipe_refresh(true); webview_set_borderless(true); webview_button_set_auto_close(webview_button_create(30, WebViewButtonGravity.CenterHorizontal | WebViewButtonGravity.Top), true); } else { url_open(path_); } } else { url_open(path_); } }
}

///@desc Manages state for UI tabs.
function ui_manage() {
	//live_auto_call 
	#region Update Text
		var update_text = function() { //Update text function
			undo_stack_begin_move(); 
				with ( obj_system ) {
					var txt = new TextChange(textinput.GetValue(), textinput.GetCaret());
					undo_stack_apply_change(txt); 
				}
			undo_stack_complete_move();
					
			if ( !bord_visible ) { sfx_play(snd_enc1, 0, , 1.3); bord_visible = true; }
		}
				
		if ( textinput.IsFocused() ) { //If the input box is in focus
			var upd_ = false, keycur = keyboard_key;
			if ( keyboard_check(vk_anykey) && keycur != 0 ) { 
				switch ( keycur ) { //Banned keys
					case vk_shift: case vk_control: case vk_lcontrol: case vk_rcontrol: case 20: case vk_alt: case vk_lalt: case vk_ralt:
					case vk_up: case vk_down: case vk_left: case vk_right: case vk_escape: case vk_pageup: case vk_pagedown: { upd_ = false; } break;
					default: { upd_ = true; }
				}
				if ( upd_ ) { dial_updatet = dial_updatet_max; } //Start timer
			}
			
			if ( is_android() ) { 
				if ( keyboard_check_pressed(vk_backspace) ) { var c_ = textinput.GetCaret(), str_ = string_delete(textinput.GetValue(), c_, 1); textinput.SetValue(str_); textinput.SetCaret(c_ - 1); keyboard_string = ""; sfx_play(snd_bump, , , random_range(0.7, 1.3)); }
				if ( keyboard_string != "" ) { 
					var c_ = textinput.GetCaret(), str_ = string_insert(ui_captial ? string_upper(keyboard_string) : keyboard_string, textinput.GetValue(), c_ + 1); textinput.SetValue(str_); textinput.SetCaret(c_ + 1);
					keyboard_string = ""; 
				}
				
				//Since virtual keyboards are weird, we have to catch whether the textbox was updated or not
				var previnput = textinput.GetValue(), getnext = soup_checkout("nextinput", false, true);
				if ( !is_undefined(getnext) && previnput != getnext ) { sfx_play(snd_txttype, , , random_range(0.7, 1.3)); dial_updatet = dial_updatet_max; upd_ = true; }
				soup_store("nextinput", textinput.GetValue(), , true);
				
				#region Bring Up Context Menu
					if ( textinput.GetSelection().has_selection ) {
						if ( !mouse_check ) {
							soupy_alarm("contextmenu", 15);
							if ( soupy_alarm_moment("contextmenu", 0) ) { sfx_play(snd_select); soup_store("rightclick", , , true); }
						}
					}
					else { soupy_alarm_set("contextmenu", "timer", 15); }
				#endregion
			}
					
			if ( keyboard_check_pressed(vk_anykey) ) { //Typing sounds
				if ( upd_ ) { sfx_play(snd_txttype, , , random_range(0.7, 1.3)); } //Play typing sounds for unbanned keys
				else {
					switch ( keycur ) { //Play unique sounds
						case vk_shift: case 20: { sfx_play(snd_equip, , , random_range(0.7, 1.3)); } break;
						case vk_control: case vk_lcontrol: case vk_rcontrol: { sfx_play(snd_enc1, , , random_range(0.7, 1.3)); } break;
						case vk_up: case vk_down: case vk_left: case vk_right: { sfx_play(snd_txttype, , , 1.5); } break;
					}
				}
				if ( keyboard_check_pressed(vk_space) ) { audio_stop_sound(snd_txttype); sfx_play(snd_bump, , , random_range(0.7, 1.3)); }
			}
			if ( keyboard_check(vk_control) && keyboard_check_pressed(ord("Z")) ) { //Undo/ redo
				if ( !keyboard_check(vk_shift) ) { undo_stack_undo(); } else { undo_stack_redo(); } 
			}
			if ( keyboard_check(vk_control) && keyboard_check_pressed(ord("D")) ) { soupy_context_page(); } //Insert Page Break
			if ( keyboard_check(vk_control) && keyboard_check_pressed(ord("P")) ) { soupy_context_macro(); } //Text Macro
		}
	#endregion

	#region Switch between pages
		if ( UI_MESSAGE ) {
			#region Left Page
				if ( dial_text_page_c > 1 && dial_text_page > 0 ) { 
					if ( variable_instance_get(obj_system, "within_hover4") == undefined ) { variable_instance_set(obj_system, "within_hover4", false); }
					if ( variable_instance_get(obj_system, "yscale_4") == undefined ) { variable_instance_set(obj_system, "yscale_4", 1); }
		
					var x_ = 10, y_ = 400, within_ = range_within(mouse_x_gui, x_ - 20, x_ + 40) && range_within(mouse_y_gui, y_ - 30, y_ + 30);
					if ( within_ ) {
						if ( !within_hover4 ) { within_hover4 = true; sfx_play(snd_sel_switch); } //Hover
						if ( mouse_pressed ) { if ( !bord_visible ) { sfx_play(snd_enc1, 0, , 1.3); bord_visible = true; } sfx_play(snd_bump, , 0.7, 1.5); sfx_play(snd_throw); dial_text_page = approach(dial_text_page, 0, 1); yscale_4 = 0.5; } //Pressed
						if ( mouse_pressed_right ) { if ( !bord_visible ) { sfx_play(snd_enc1, 0, , 1.3); bord_visible = true; } sfx_play(snd_bump, , 0.7, 1.5); sfx_play(snd_throw, , , 1.3); dial_text_page = 0; } //Pressed Right
					}
					else { within_hover4 = false; }
					soupyclipm_begin_clip();
						draw_sprite_stretched_ext(spr_pixel, 0, ( x_ - 22 ) + 13, y_ - 17, 39, 34, c_white, 1);
					soupyclipm_end_clip();
							
					soupyclipm_draw();
						draw_sprite_stretched_ext(spr_pixel, 0, x_ - 22, y_ - 17, 39, 34, c_black, 1); //Outline
						draw_sprite_stretched(spr_border_undertale, 0, x_ - 20, y_ - 15, 35, 30); //Border
						draw_sprite_ensure(spr_effects_icons, 12, x_ - abs(sin(current_time/250) ) * 4, y_, -1, yscale_4, 180, within_ ? c_white : c_yellow); //Right Arrow
					shader_reset();
					yscale_4 = lerp(yscale_4, 1, 0.15);
				}
			#endregion
			#region Right Page
				if ( dial_text_page_c > 1 && dial_text_page < ( dial_text_page_c - 1 ) ) {
					if ( variable_instance_get(obj_system, "within_hover5") == undefined ) { variable_instance_set(obj_system, "within_hover5", false); }
					if ( variable_instance_get(obj_system, "yscale_5") == undefined ) { variable_instance_set(obj_system, "yscale_5", 1); }
		
					var x_ = 630, y_ = 400, within_ = range_within(mouse_x_gui, x_ - 40, x_ + 20) && range_within(mouse_y_gui, y_ - 30, y_ + 30);
					if ( within_ ) {
						if ( !within_hover5 ) { within_hover5 = true; sfx_play(snd_sel_switch); } //Hover
						if ( mouse_pressed ) { if ( !bord_visible ) { sfx_play(snd_enc1, 0, , 1.3); bord_visible = true; } sfx_play(snd_bump, , 0.7, 1.5); sfx_play(snd_throw); dial_text_page = approach(dial_text_page, dial_text_page_c, 1); yscale_5 = 0.5; } //Pressed
						if ( mouse_pressed_right ) { if ( !bord_visible ) { sfx_play(snd_enc1, 0, , 1.3); bord_visible = true; } sfx_play(snd_bump, , 0.7, 1.5); sfx_play(snd_throw, , , 1.3); dial_text_page = dial_text_page_c - 1; } //Pressed Right
					}
					else { within_hover5 = false; }
					soupyclipm_begin_clip();
						draw_sprite_stretched_ext(spr_pixel, 0, x_ - 15, y_ - 17, 24, 34, c_white, 1);
					soupyclipm_end_clip();
							
					soupyclipm_draw();	
						draw_sprite_stretched_ext(spr_pixel, 0, x_ - 15, y_ - 17, 39, 34, c_black, 1); //Outline
						draw_sprite_stretched(spr_border_undertale, 0, x_ - 13, y_ - 15, 35, 30); //Border
						draw_sprite_ensure(spr_effects_icons, 12, x_ + abs(sin(current_time/250) ) * 4, y_, , yscale_5, 180, within_ ? c_white : c_yellow); //Right Arrow
					shader_reset();
					yscale_5= lerp(yscale_5, 1, 0.15);
				}
			#endregion
			#region Page Indicator Text
				if ( dial_text_page_c > 1 && bord_visible ) {
					var pageind = scribble($"< Page {dial_text_page + 1}/ {dial_text_page_c} > [offset,0,-2][spr_effects_icons,16][offsetpop]")
											.starting_format("fnt_abaddon", c_gray)
											.align(fa_center, fa_middle)
											.draw(320, 333)
				}
			#endregion
		}
	#endregion
			
	#region Textbox and Quick Text
		QuillDrawOverlays();
		
		draw_format("left", "center", fnt_abaddon);
		draw_text_ext(20, 90, "Quick Colors:\n \nQuick Effects:", 12, -1);
	#endregion
			
	#region Color and Effects Function
		if ( variable_instance_get(obj_system, "butt_func") == undefined ) { variable_instance_set(obj_system, "butt_func", method({textinput, update_text, typist_spd}, function (data_, color_ = false) { //Button data
			#region Commands with extra parameters
				var extra_;
				switch ( data_ ) {
					case "scale": case "wait": case "alpha": { extra_ = ",0.5"; } break;
					case "cycle": { extra_ = ",0,120"; } break;
					case "offset": { extra_ = ",24,24"; } break;
					case "speed": { extra_ = $",{typist_spd}"; } break;
					default: { extra_ = ""; }
				}
			#endregion
					
			textinput.Focus();

			var txt_ = textinput.GetValue(), txt_insert = $"[{data_}{extra_}]", txt_insert_end = color_ ? "[/c]" : $"[/{data_}]", result;
			var getpos_ = textinput.GetSelection() ,pos_ = getpos_.start + 1, pos_2 = getpos_._end + 1; //Get the current cursor's position and highlighted position
			if ( !getpos_.has_selection ) { //Not trying to highlight anything
				result = string_insert(txt_insert, txt_, pos_);
			}
			else { //Between highlighted text
				result = string_insert(txt_insert, string_insert(txt_insert_end, txt_, pos_2), pos_);
			}
			textinput.SetValue(result); textinput.SetCaret(pos_ - 1); update_text();
			sfx_play(snd_bump, , , 1.5); audio_stop_sound(snd_updated);
		})); }
	#endregion

	#region Color Buttons
		if ( variable_instance_get(obj_system, "colors_get") == undefined ) { variable_instance_set(obj_system, "colors_get", __scribble_config_colours()); }
		draw_sprite_ext(spr_pixel, 0, 158 - 2, 68 - 2, 429 + 4, 14 + 4, 0, c_white, 1); //Palette Outline White
		draw_sprite_ext(spr_pixel, 0, 158, 68, 429, 14, 0, rgb(39, 31, 54), 1); //Palette Back
		var colors_ = ["c_red", "c_yellow", "c_blue", "c_lime", "c_aqua", "c_cyan", "c_purple", "c_orange", "c_maroon", "c_pink", "c_gold", "c_white", "c_ltgray", "c_gray", "c_dkgray", "c_black"], colors_i = 0, colors_len = array_length(colors_); //Available colors
		repeat ( colors_len ) {
			var colors_cur = colors_[colors_i]; //Current color
			var butt_data = { x: 160 + ( 27 * colors_i ), y: 70, sprite: spr_color_button, draw_nine: false, leeway: 3, color_butt: colors_get[$ colors_cur], color_butt_hover: merge_color(colors_get[$ colors_cur], color_get_value(colors_get[$ colors_cur]) > 150 ? c_black : c_white, 0.3), on_click: method({ colors_cur }, function () { SYSTEMUI.butt_func(colors_cur, true); }), on_click_right: method({ colors_cur }, function () { 
				sfx_play(snd_equip2, , , 1.5); 
				var clrget = SYSTEMUI.colors_get[$ colors_cur];
				if ( SYSTEMUI.dial_text_outline != clrget ) { if ( !string_search(SYSTEMUI.dial_font, "_outline", true) ) { SYSTEMUI.dial_font = $"{SYSTEMUI.dial_font}_outline"; } SYSTEMUI.dial_text_outline = clrget; } //Switching to a new color? Change the text outline, otherwise disable text outline
				else { SYSTEMUI.dial_font = string_replace(SYSTEMUI.dial_font, "_outline", ""); SYSTEMUI.dial_text_outline = -1; }
			}) };
					
			butt_data[$ "x2"] = butt_data.x + sprite_get_width(butt_data.sprite); butt_data[$ "y2"] = butt_data.y + sprite_get_height(butt_data.sprite); 
			var butt_ = new Button(butt_data); butt_.update(); //Create button
		colors_i++; }
	#endregion
			
	#region Effects Buttons
		var effects_ = ["Wave   ", "Wheel    ", "Shake ", "Wobble  ", "Pulse ", "Rainbow", "Slant ", "Scale    ", "Cycle  ", "Blink    ", "Alpha  ", "Speed    "], effects_i = 0, effects_len = array_length(effects_), effects_off = effects_len - 6; //Available effects
		repeat ( effects_len ) {
			if ( effects_i > 5 ) { continue; }
			var effects_true = effects_i + ui_effoff;
			var effects_cur = effects_[effects_true]; //Current effect
			var butt_data = { x: 180 + ( 75 * effects_i ), y: 95, color_butt: ui_accentcolor, color_butt_hover: c_yellow, color: c_black, text: $"{effects_cur} [spr_effects_icons,{effects_true}]", padd_multi: 4, on_hover: undefined, on_click: method({ effects_cur }, function () { SYSTEMUI.butt_func(string_letters(string_lower(effects_cur))); }) } 
			var butt_ = new Button(butt_data); butt_.update(); //Create button
		effects_i++; }
				
		if ( UI_MESSAGE ) {
			#region Right Button
				if ( variable_instance_get(obj_system, "within_hover") == undefined ) { variable_instance_set(obj_system, "within_hover", false); }
				if ( variable_instance_get(obj_system, "yscale_") == undefined ) { variable_instance_set(obj_system, "yscale_", 1); }
				if ( ui_effoff < effects_off ) {
					var x_ = 605, y_ = 98, within_ = range_within(mouse_x_gui, x_ - 10, 640) && range_within(mouse_y_gui, y_ - 10, y_ + 10);
					if ( within_ ) {
						if ( !within_hover ) { within_hover = true; sfx_play(snd_sel_switch); } //Hover
						if ( mouse_pressed ) { sfx_play(snd_sel_switch, 0, , 1.3); ui_effoff = approach(ui_effoff, effects_off, 1); yscale_ = 0.5; } //Pressed
						if ( mouse_pressed_right ) { sfx_play(snd_throw, 0, , 1.3); sfx_play(snd_bump, , 0.7, 1.5); ui_effoff = effects_off; } //Pressed Right
					}
					else { within_hover = false; }
					draw_sprite_ensure(spr_effects_icons, 12, x_ + ( abs(sin(current_time/300) * 5) ) , y_, -1, yscale_, , within_ ? c_white : c_yellow); //Right Arrow
				}
				yscale_ = lerp(yscale_, 1, 0.15);
			#endregion
			#region Left Button
				if ( variable_instance_get(obj_system, "within_hover2") == undefined ) { variable_instance_set(obj_system, "within_hover2", false); }
				if ( variable_instance_get(obj_system, "yscale_2") == undefined ) { variable_instance_set(obj_system, "yscale_2", 1); }
				if ( ui_effoff > 0 ) {
					var x_ = 130, y_ = 98, within_ = range_within(mouse_x_gui, x_ - 40, x_ + 10) && range_within(mouse_y_gui, y_ - 10, y_ + 10);
					if ( within_ ) {
						if ( !within_hover2 ) {within_hover2 = true; sfx_play(snd_sel_switch); } //Hover
						if ( mouse_pressed ) { sfx_play(snd_sel_switch, 0, , 0.7); ui_effoff = approach(ui_effoff, 0, 1); yscale_2 = 0.5; } //Pressed
						if ( mouse_pressed_right ) { sfx_play(snd_throw, 0, , 1.3); sfx_play(snd_bump, , 0.7, 1.5); ui_effoff = 0; } //Pressed Right
					}
					else { within_hover2 = false; }
					draw_sprite_ensure(spr_effects_icons, 12, x_ - ( abs(sin(current_time/300) * 5) ), y_, , yscale_2, , within_ ? c_white : c_yellow); //Left Arrow
				}
				yscale_2 = lerp(yscale_2, 1, 0.15);
			#endregion
		}
	#endregion

	#region Text Update
		if ( dial_updatet > 1 && !textinput.ContextMenuIsOpened() ) { //Notification for updating text
			dial_updatet--;
			var ringcalc = map_value(dial_updatet, 0, dial_updatet_max, 0, 360), textx = 300, texty = 395; //Turn the values of a timer into a range of degrees
					
			var ninesl_ = sprite_get_nineslice(spr_bord), off_ = spr_bord == spr_border_deltarune ? 15 : 5, mybord = global.pref.anyborder ? spr_border_undertale_safe : spr_bord;
			if ( ninesl_.enabled && !global.pref.anyborder ) { draw_sprite_stretched_ext(mybord, bord_index, ( textx - 110 ) - off_, ( texty - 20 ) - off_, 250 + ( off_ * 2 ), 40 + ( off_ * 2 ), bord_clr, 1); } else { draw_9slice(mybord, bord_index, textx - 110, texty - 20, 250, 40, bord_clr, bord_scale, bord_stretch); } //Dialogue Box
			var updatering = CleanRing(textx + 115, texty, 5, 10, 360, ringcalc) //Update text ring
												.Blend(c_yellow, 1)
												.Draw();
													
			draw_format(fa_center, fa_middle, fnt_speech, c_yellow);
			draw_text(textx, texty, "Live-updating text...!");
					
			if ( mouse_pressed && ( range_within(mouse_x_gui, ( textx - 110 ) - 20, ( ( textx - 110 ) + 250 ) + 20) && range_within(mouse_y_gui, ( texty - 20 ) - 20, ( (texty - 20 ) + 40 ) + 20) ) ) { dial_updatet = 1; } //Early regeneration
		}
		else { if ( dial_updatet == 1 ) { dial_updatet = 0; update_text(); } } //Update the text
	#endregion
			
	#region Change Cursor
		if ( UI_MESSAGE ) {
			if ( range_within(mouse_x_gui, 120, 620) && range_within(mouse_y_gui, 60, 120) ) { window_set_cursor(cr_drag); } //At the command palette
			else if ( range_within(mouse_x_gui, 20, 620) && range_within(mouse_y_gui, 110, 300) ) { window_set_cursor(cr_beam); } //At the textbox
			else { if ( mouse_y_gui >= 60 ) { window_set_cursor(cr_default); } }
		}
	#endregion
			
	#region Clear Page Face
		var resettime = 60;
		if ( bord_visible && FACE_CURRENT != -1 && ( range_within(mouse_x_gui, 40 + dial_face_xoff_static, 174 + dial_face_xoff_static) && range_within(mouse_y_gui, 323 + dial_face_yoff_static, 480 + dial_face_yoff_static) ) && mouse_check_right ) { //Hovering over the dialogue portrait
			if ( variable_instance_get(obj_system, "within_hoverindex") != undefined && !within_hoverindex ) && ( variable_instance_get(obj_system, "within_hoverindex2") != undefined && !within_hoverindex2 ) {
				soupy_alarm("removeface", resettime);
				soupy_alarm_run("removeface", 1, function () { FACE_CURRENT = -1; FACE_ORIGINAL = -1; FACE_PREVIOUS = -1; sfx_play(snd_hurtpowerful); }); //Timer to clear face

				draw_sprite_stretched_ext(spr_border_undertale, 0, 40 + dial_face_xoff_static, 323 + dial_face_yoff_static, 134, 136, c_red, 0.7); //BG
				var ringcalc = map_value(soupy_alarm_get("removeface", "timer", false), 0, resettime, 0, 360), textx = 110 + dial_face_xoff_static, texty = 390 + dial_face_yoff_static; //Turn the values of a timer into a range of degrees
				var updatering = CleanRing(textx, texty, 20, 30, 360, ringcalc) //Update text ring
													.Blend(c_red, 1)
													.Draw();
				draw_format(fa_center, fa_middle, fnt_speech, c_red);
				draw_text(textx, texty, "Clearing\n\n\n\n\n\nface...");
			}
		}
		else { soupy_alarm_set("removeface", "timer", resettime); }
	#endregion
			
	#region Quick Index & Sprite Switching 
		if ( UI_MESSAGE && bord_visible && ( range_within(mouse_x_gui, 20 + dial_face_xoff_static, 180 + dial_face_xoff_static) && range_within(mouse_y_gui, 300 + dial_face_yoff_static, 480 + dial_face_yoff_static) ) ) { //Hovering over the dialogue portrait
			#region Back Index
				if ( FACE_SPEED == 0 && FACE_INDEX > 0 && FACE_CURRENT != -1 ) { 
					if ( variable_instance_get(obj_system, "within_hoverindex") == undefined ) { variable_instance_set(obj_system, "within_hoverindex", false); }
					if ( variable_instance_get(obj_system, "yscale_index") == undefined ) { variable_instance_set(obj_system, "yscale_index", 1); }
					
					var x_ = 110 + dial_face_xoff_static, y_ = 325 + dial_face_yoff_static, within_ = range_within(mouse_x_gui, x_ - 20, x_ + 20) && range_within(mouse_y_gui, y_ - 20, y_ + 10);
					if ( within_ ) {
						if ( !within_hoverindex ) { within_hoverindex = true; sfx_play(snd_sel_switch); } //Hover
						if ( mouse_pressed ) {  sfx_play(snd_bump, 0, , 1.3); FACE_INDEX = approach(FACE_INDEX, 0, 1); yscale_index = 0.5; } //Pressed
						if ( mouse_pressed_right ) { io_clear(); sfx_play(snd_throw, 0, , 1.3); FACE_INDEX = 0; yscale_index = 0.5; } //Pressed Right
					}
					else { within_hoverindex = false; }
					draw_sprite_ensure(spr_effects_icons, 12, x_, y_ + ( abs(sin(current_time/ 200)) * 5 ), , yscale_index, 270, within_ ? c_yellow : c_cyan); // Arrow
					yscale_index = lerp(yscale_index, 1, 0.15);
				}
				else { variable_instance_set(obj_system, "within_hoverindex", false); }
			#endregion
			#region Forward Index
				if ( FACE_SPEED == 0 && FACE_INDEX < sprite_get_number(FACE_CURRENT) - 1 && FACE_CURRENT != -1 ) {
					if ( variable_instance_get(obj_system, "within_hoverindex2") == undefined ) { variable_instance_set(obj_system, "within_hoverindex2", false); }
					if ( variable_instance_get(obj_system, "yscale_index2") == undefined ) { variable_instance_set(obj_system, "yscale_index2", 1); }
					
					var x_ = 110 + dial_face_xoff_static, y_ = 457 + dial_face_yoff_static, within_ = range_within(mouse_x_gui, x_ - 20, x_ + 20) && range_within(mouse_y_gui, y_ - 20, y_ + 10);
					if ( within_ ) {
						if ( !within_hoverindex2 ) { within_hoverindex2 = true; sfx_play(snd_sel_switch); } //Hover
						if ( mouse_pressed ) {  sfx_play(snd_bump, 0, , 1.3); FACE_INDEX = approach(FACE_INDEX, sprite_get_number(FACE_CURRENT) - 1, 1); yscale_index2 = 0.5; } //Pressed
						if ( mouse_pressed_right ) { io_clear(); sfx_play(snd_throw, 0, , 1.3); FACE_INDEX = sprite_get_number(FACE_CURRENT) - 1; yscale_index2 = 0.5; } //Pressed Right
					}
					else { within_hoverindex2 = false; }
					draw_sprite_ensure(spr_effects_icons, 12, x_, y_ - ( abs(sin(current_time/ 200)) * 5 ), , yscale_index2, 90, within_ ? c_yellow : c_cyan); // Arrow
					yscale_index2 = lerp(yscale_index2, 1, 0.15);
				}
				else { variable_instance_set(obj_system, "within_hoverindex2", false); }
			#endregion
			#region Switch Sprite
				if ( keyboard_check(vk_control) ) { if ( mouse_pressed ) { external_choose_face(); } else if ( mouse_pressed_right ) { soupy_color_picker_portrait(); } }
			#endregion
		}
		
		if ( UI_MESSAGE && bord_visible && ( range_within(mouse_x_gui, 190, 640) && range_within(mouse_y_gui, 315, 480) ) ) { //Hovering over the dialogue border
			if ( keyboard_check(vk_control) ) { if ( mouse_pressed ) { external_choose_border(); } else if ( mouse_pressed_right ) { soupy_color_picker_border(); } }
		}
	#endregion
			
	#region Toggle Dialogue Box Visibility
		if ( UI_MESSAGE ) {
			if ( variable_instance_get(obj_system, "within_hover3") == undefined ) { variable_instance_set(obj_system, "within_hover3", false); }
			if ( variable_instance_get(obj_system, "yscale_3") == undefined ) { variable_instance_set(obj_system, "yscale_3", 1); }
		
			var x_ = 320, y_ = 473, within_ = range_within(mouse_x_gui, x_ - 40, x_ + 40) && range_within(mouse_y_gui, y_ - 40, y_ + 50);
			if ( within_ ) {
				if ( !within_hover3 ) { within_hover3 = true; sfx_play(snd_sel_switch); } //Hover
				if ( mouse_pressed ) {  sfx_play(snd_enc1, 0, , bord_visible ? 0.7 : 1.3); bord_visible = !bord_visible; yscale_3 = 0.5; } //Pressed
			}
			else { within_hover3 = false; }
			draw_sprite_ensure(spr_effects_icons, 12, x_, y_, , yscale_3, bord_visible ? 90 : 270, within_ ? c_white : c_yellow); //Left Arrow
			yscale_3 = lerp(yscale_3, 1, 0.15);
		}
	#endregion
			
	#region Create Mini Face
		if ( UI_MESSAGE ) {
			var xx_ = 632, yy_ = 470, within_ = range_within(mouse_x_gui, xx_ - 60, xx_ + 20) && range_within(mouse_y_gui, yy_ - 60, yy_ + 20);
			if ( variable_instance_get(obj_system, "within_mini") == undefined ) { variable_instance_set(obj_system, "within_mini", false); }
			if ( variable_instance_get(obj_system, "within_mini_off") == undefined ) { variable_instance_set(obj_system, "within_mini_off", false); }
			if ( within_ ) {
				if ( !within_mini ) { within_mini = true; sfx_play(snd_sel_switch); } //Hover
				if ( mouse_pressed ) { //Pressed
					sfx_play(snd_select);
					external_choose_mini();
				}
			}
			else { within_mini = false; }
			within_mini_off = lerp(within_mini_off, within_ ? 15 : 0, 0.30);

			#region Sprites
				soupyclipm_begin_clip();
					draw_set_color(c_white); draw_rectangle(590, 430, 639, 479, false);
				soupyclipm_end_clip();

				soupyclipm_draw();
					draw_sprite_stretched_ext(spr_border_undertale_outlined, 0, ( xx_ - 17 ) - within_mini_off, ( yy_ - 17 ) - within_mini_off, 50, 50, within_ ? c_yellow : c_white, 1);
					draw_sprite_ensure(spr_gui_icons, 7, ( xx_ - within_mini_off ) + ( within_ ? 8 : 0 ), ( yy_ - within_mini_off ) + ( within_ ? 8 : 0 ), within_ ? 2 : 1, within_ ? 2 : 1, , within_ ? c_yellow : c_white);
				shader_reset();
			#endregion
		}
	#endregion
}
